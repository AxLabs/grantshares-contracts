plugins {
    id 'java-library'
    id 'io.neow3j.gradle-plugin' version '3.17.0-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group 'com.axlabs.neo'

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

sourceSets {
    deploy {
        compileClasspath += sourceSets.test.output
        runtimeClasspath += sourceSets.test.output
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

dependencies {
    implementation 'io.neow3j:devpack:3.17.0-SNAPSHOT'

    testImplementation 'io.neow3j:devpack-test:3.17.0-SNAPSHOT',
            'org.junit.jupiter:junit-jupiter:5.8.2',
            'org.hamcrest:hamcrest:2.2'

    deployImplementation 'io.neow3j:compiler:3.17.0-SNAPSHOT'
}

tasks.withType(Test) {
    useJUnitPlatform()
}

neow3jCompile {
    className = "com.axlabs.neo.grantshares.GrantSharesTreasury"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task testJar(type: Jar, dependsOn: testClasses) {
    classifier 'tests'
    from sourceSets.test.output
}

artifacts{
    archives sourcesJar
    archives testJar
}

publishing {
    publications {
        myLibrary(MavenPublication) {
            groupId "${project.group}"
            artifactId "${project.name}"

            from components.java
            artifact sourcesJar
            artifact testJar
        }
    }
}